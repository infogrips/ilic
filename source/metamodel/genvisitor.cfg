PROCEDURE VISIT_CLASS

   ! struct DocText : public MMObject {
   VAR.BUFFER
      EXTRL POP
      EXTRL TRIM => LOCAL.CLASS
   TRIM => LOCAL.BUFFER
   IF LOCAL.BUFFER ':' STARTS_WITH THEN
      LOCAL.BUFFER
         EXTRL POP
         EXTRL POP
         EXTRL TRIM => LOCAL.BASE_CLASS
   ELSE
      '' => LOCAL.BASE_CLASS
   END_IF
   
   DISPLAY 'static void visit',LOCAL.CLASS,'(MetaModelVisitor *visitor, ',LOCAL.CLASS,' *t)'
   DISPLAY '{'
   DISPLAY ''
   DISPLAY '   /*'
   DISPLAY VAR.BUFFER
   
   MAPCREATE => LOCAL.ATTRS
   WHILE VAR.F TEXTFILE.READLN DO
      DUP => LOCAL.BUFFER_ORG
      TRIM => LOCAL.BUFFER
      IF LOCAL.BUFFER = '};' THEN
         BREAK
      ELSIF LOCAL.BUFFER 'virtual' STARTS_WITH THEN
         CONTINUE
      ELSIF LOCAL.BUFFER 'list' STARTS_WITH THEN
         ! list<MetaAttribute *> MetaAttribute;
         LOCAL.BUFFER 4 EXTRLN POP TRIM => LOCAL.BUFFER
         LOCAL.BUFFER
            EXTRL TRIM => LOCAL.TYPE
            EXTRL POP
            TRIM => LOCAL.ATTR
         LOCAL.TYPE 1 EXTRLN POP EXTRL1 => LOCAL.TYPE
         LOCAL.ATTR 1 EXTRRN POP => LOCAL.ATTR
         &LOCAL.ATTRS LOCAL.ATTR LOCAL.TYPE MAPINS
      END_IF
      DISPLAY LOCAL.BUFFER_ORG
   END_WHILE

   DISPLAY '   */'
   DISPLAY ''
   DISPLAY '   if (!visitor->visit',LOCAL.CLASS,'Override()) {'
   DISPLAY '      return;'
   DISPLAY '   }'

   DISPLAY ''
   DISPLAY '   display_debug(visitor,t);'
   IF LOCAL.BASE_CLASS <> '' THEN
      DISPLAY '   visitor->visit',LOCAL.BASE_CLASS,'(t);'
   END_IF
   DISPLAY '   visitor->visit',LOCAL.CLASS,'(t);'

   &LOCAL.ATTRS MAPRESET
   WHILE &LOCAL.ATTRS MAPSCAN DO
      => LOCAL.ATTR
      => LOCAL.TYPE
      DISPLAY ''
      DISPLAY '   for (auto v : t->',LOCAL.ATTR,') {'
      DISPLAY '      visit',LOCAL.TYPE,'(visitor,v);'
      DISPLAY '   }'
   END_WHILE

   DISPLAY ''
   DISPLAY '}'
   DISPLAY ''

END_PROCEDURE

! main
'MetaModel.h' TEXTFILE.OPEN POP => VAR.F
WHILE VAR.F TEXTFILE.READLN DO
   => VAR.BUFFER
   VAR.BUFFER TRIM => VAR.TRIMMED_BUFFER
   ! class Constraint : public MetaElement { // ABSTRACT, 2.4
   IF VAR.TRIMMED_BUFFER 'class' STARTS_WITH
      VAR.TRIMMED_BUFFER '{' LOC SWAP POP IS_NOT_NULL AND
   THEN
      VISIT_CLASS
   ELSIF VAR.TRIMMED_BUFFER 'struct' STARTS_WITH 
      VAR.TRIMMED_BUFFER '{' LOC SWAP POP IS_NOT_NULL AND
   THEN
      VISIT_CLASS
   END_IF
END_WHILE
VAR.F TEXTFILE.CLOSE